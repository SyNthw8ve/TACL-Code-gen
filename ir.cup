
import java_cup.runtime.*;

import java.io.InputStreamReader;
import classes.other.*;
import classes.nodes.*;

import java.util.LinkedList;
import java.util.List;

parser code {:

  SymbolTable symbol_table = new SymbolTable();

  public static void main(String args[]) throws Exception {

    IR ir = new IR(new Yylex(new InputStreamReader(System.in)));

    Symbol result = null;

    try {

      result = ir.parse();

    }
    catch(Exception e) {

      System.err.print("** Caught: ");

      e.printStackTrace();
    }

    if (result != null && result.sym == sym.EOF)
      System.exit(0);
    else
      System.exit(1);
  }

  public void report_error(String message, Object error_info) {
    Symbol info = (Symbol) error_info;

    System.err.print(message + ": line " + (info.right + 1) + ": " );
    System.err.println("unexpected token `" + info.value + "'");
  }

  public void report_fatal_error(String message, Object _ignore) {
    System.err.println(message);
  }
:};

terminal String ID;
terminal Integer I_LIT, B_LIT;
terminal LS_PAR, RS_PAR, L_PAR, R_PAR, COL, COMMA, NEG;
terminal ID_K, VAR, FUN, FUNCTION, INT, BOOL, VOID;
terminal I_TEMP, LABEL;
terminal IR_STORE;

terminal I_ADD, I_SUB, I_MUL, I_DIV, I_INV, I_COPY, MOD;
terminal I_EQ, I_NE, I_LT, I_LE;

terminal I_VAL, I_GLOAD, I_LLOAD, I_ALOAD, I_GSTORE, I_LSTORE, I_ASTORE;
terminal I_CALL, I_RET;

terminal P_CALL, P_RET;

terminal NOT;

terminal JUMP, CJUMP;
terminal I_PRINT, B_PRINT, I_READ, B_READ;

non terminal program;
non terminal global_decs, global_dec;
non terminal Integer intial_value;
non terminal List<Pair> formals, locals;
non terminal Pair pair;
non terminal ir_decs, ir_dec;

non terminal ir_statements, ir_statement, ir_expr, int_expr, jump_expr, ret_expr, call_expr;
non terminal type, function_head, function_body;
non terminal int_temp, actual_arg, actual_args, more_actual_args, io_expr;

program ::= global_decs ir_decs:i {::};

global_decs ::=    	global_dec:d global_decs:ds {: :}
                	|   
					;

global_dec ::=  L_PAR ID_K ID:s VAR type:t R_PAR {:Info i = new Info(Info.Type.VAR, null, null, null); parser.symbol_table.add(s, i);:}
			|	L_PAR ID_K ID:s VAR type:t intial_value:v R_PAR {:Info i = new Info(Info.Type.VAR, null, null, v); parser.symbol_table.add(s, i);:}
			|	L_PAR ID_K ID FUN type LS_PAR formals RS_PAR LS_PAR locals RS_PAR R_PAR
			;

ir_decs ::= 	ir_dec ir_decs
			|	
			;

ir_dec ::= 	function_head function_body
			;

function_head ::= FUNCTION ID;

function_body ::= ir_statements
				;

ir_statements ::= LABEL COL	ir_statement ir_statements
				| ir_statement ir_statements
				| 
				;

ir_statement ::= 	ir_expr
				;


ret_expr ::=	I_RET int_temp
			|	P_RET
			;

int_temp ::=	I_TEMP
			;


ir_expr ::= 	int_expr
			|	jump_expr
			|	call_expr
			|	io_expr
			|	ret_expr
			;

int_expr ::=	int_temp IR_STORE I_VAL I_LIT
			|	int_temp IR_STORE I_GLOAD ID
			|	int_temp IR_STORE I_LLOAD ID
			|	int_temp IR_STORE I_ALOAD ID
			|	ID IR_STORE I_GSTORE int_temp
			|	ID IR_STORE I_LSTORE int_temp
			|	ID IR_STORE I_ASTORE int_temp
			|	int_temp IR_STORE I_ADD int_temp COMMA int_temp
			|	int_temp IR_STORE I_SUB int_temp COMMA int_temp
			|	int_temp IR_STORE I_MUL int_temp COMMA int_temp
			|	int_temp IR_STORE I_DIV int_temp COMMA int_temp
			|	int_temp IR_STORE MOD int_temp COMMA int_temp
			|	int_temp IR_STORE I_INV int_temp
			|	int_temp IR_STORE NOT int_temp
			|	int_temp IR_STORE I_EQ int_temp COMMA int_temp
			|	int_temp IR_STORE I_LT int_temp COMMA int_temp
			|	int_temp IR_STORE I_NE int_temp COMMA int_temp
			|	int_temp IR_STORE I_LE int_temp COMMA int_temp
			|	int_temp IR_STORE I_COPY int_temp
			;

jump_expr ::=	CJUMP int_temp COMMA LABEL COMMA LABEL
			|	JUMP LABEL
			;

call_expr ::= 	int_temp IR_STORE I_CALL ID COMMA LS_PAR actual_args RS_PAR
			|	P_CALL ID LS_PAR actual_args RS_PAR
			;

actual_arg ::= 	int_temp
			;

more_actual_args ::= COMMA actual_arg more_actual_args
				|
				;

actual_args ::= actual_arg more_actual_args
			|
			;

io_expr ::= I_PRINT int_temp
		|	B_PRINT int_temp
		|	int_temp IR_STORE I_READ
		|	int_temp IR_STORE B_READ
		;

type ::= 	INT {::}
		|	BOOL {::}
		|	VOID {::}
		;

intial_value ::= 	I_LIT:i {:RESULT = i;:}
				|  	NEG I_LIT:i {:RESULT = -1*i;:}
				|	B_LIT:b {:RESULT = b;:}
				;

formals ::=		pair:p formals:f {: f.add(p); RESULT = f; :}
			|	{: RESULT = new LinkedList<Pair>(); :}
			;

locals ::=		pair:p locals:l {: l.add(p); RESULT = l; :}
			|	{: RESULT = new LinkedList<Pair>(); :}
			;

pair ::= 		L_PAR INT ID:s R_PAR {: RESULT = new Pair(s, Pair.Type.INT); :}
			|	L_PAR BOOL ID:s R_PAR {: RESULT = new Pair(s, Pair.Type.BOOL); :}
			;